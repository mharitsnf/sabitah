~ start
if CollectibleState.get_collectible_status("boat_key") != CollectibleState.CollectibleStatus.OBTAINED:
	if !ProgressState.get_progress(["tutorial_island", 'teacher', 'intro']):
		=>< intro
		=> END
	else:
		=>< offer_clue_help
		=> END

if !ProgressState.get_global_progress(["tutorial_island_registered"]):
	if !ProgressState.get_progress(["tutorial_island", 'teacher', 'sundial_intro']):
		=>< sundial_intro
		=> END
	else:
		=>< offer_sundial_help
		=> END

if !ProgressState.get_global_progress(['tutorial_island_set_as_node_island']):
	if !ProgressState.get_progress(["tutorial_island", 'teacher', 'node_island_intro']):
		=>< node_island_intro
		=> END
	else:
		=>< node_island_help
		=> END

if !ProgressState.get_progress(["tutorial_island", 'teacher', 'end_test_intro']):
	=>< end_test_intro
	=> END
else:
	=> post_tutorial
	=> END

=> END

~ intro
Teacher: There you are! You ready to get your own boat and become the world's best treasure hunter?
- Of course! I'm fully awake now!
	Teacher: Huh, that's rare... but it's great to hear that!
Teacher: But first, you know you'll need to complete the final test first, right?
- Yeah, I know, I remember that.
Teacher: Good! Cause this test will help you sharpen your treasure hunting and navigation skills so you can go anywhere in the world!
Teacher: The test is simple: I will give you a clue that will lead you to a treasure!
Teacher: All you need to do is to figure out where the treasure is based on the clue I provided.
Teacher: If you don't remember, I can explain to you how you can find treasures. Would you like me to do that?
- Yes please!
	=>< treasure_hunting_tutorial
- I think I'm good.
	Teacher: You came prepared, huh? Nice!
Teacher: I've given you the clue. You can see it in your clues journal.
do ClueState.change_clue_status('TI_teacher1', ClueState.ClueStatus.ACTIVE)
do Group.first("hud_layer").set_notification_text("New clue available!")
do Group.first("hud_layer").show_notification()
Teacher: Come back to me when you've found the treasure, and I'll continue with the next test. Good luck!
do ProgressState.progress["tutorial_island"]['teacher']['intro'] = true
=> END

~ treasure_hunting_tutorial
Teacher: First thing first, see which treasure you want to pursue by looking at the Clues Journal. You can access the journal through the main menu, by pressing the TAB key.
Teacher: Once you selected a clue, you'll see that it will come with pictures and description.
Teacher: Try to figure out the treasure based on the pictures and the description!
Teacher: Of course, if you've figured it out, you need to go there to dig it.
Teacher: Once you've reached the location of the treasure, press C, select which clue you're currently pursuing, and see if your guess is correct or not!
Teacher: Do you want me to explain this again?
- Yeah, maybe one more time?
	Teacher: No worries!
	=> treasure_hunting_tutorial
- Nah, I got it!
	Teacher: Perfect!
=> END

~ offer_clue_help
Teacher: Hey buddy! How's it going? Do you need any help?
- Can you explain to me about treasure hunting again?
	=>< treasure_hunting_tutorial
- No, I just wanna say hi to you!
	Teacher: Well, aren't you nice! But you need to focus on your test first right now.
Teacher: Good luck with the hunting! I'll be here if you need me.
=> END

~ sundial_intro
Teacher: Hey, is that what I think it is? You got it!!!
- Of course!
Teacher: Oh that's so good! I know you were gonna make it!
Teacher: But the test is not done yet. You see, if you try to use the key on your boat, it won't work! Because I messed it up a bit, haha!
- Why would you do that...?
Teacher: Because the test is not over! I have one more thing for you, and after that, I will fix the key and you'll be good to go.
Teacher: Do you remember where you got this key? It's on an island called Sundial Park! It's called that because, well, it got a sundial on it.
Teacher: I hope you know where I'm going with this, because the next test is going to be about sundial practice!
- Ugh, I should have known...
Teacher: Hey, sundial is a vital part of treasure hunting! Sailors can travel around thanks to their sundials, after all.
Teacher: You do know what kind of stuff that you can do with sundials, right?
- Um... remind me again?
Teacher: Sheesh... I know this is your least favorite subject, but it's very important, so please keep this in mind!
Teacher: There are three main things that you can do with the sundial.
Teacher: Firstly, sundials can act as a compass! It can tell you where the true north and south are.
Teacher: Secondly, you can find your current latitude with it as well!
Teacher: Finallly, you can find your current time, and therefore, your current longitude.
do ProgressState.global_progress['help_menu_unlocked'] = true
do ProgressState.global_progress['globe_menu_unlocked'] = true
do Group.first("hud_layer").set_notification_text("New menus unlocked: Globe and Help!")
do Group.first("hud_layer").show_notification()
Teacher: I know sundials can be a bit tricky, so I'm giving you the instructions on how to use it in the help page. Check the main menu later, okay?
- Okay... so what is the test?
Teacher: Your task is to register this island on your globe!
Teacher: You see, every sailor has a globe filled with interesting locations. But the globes will not fill themselves automatically, you need to know the exact coordinates of those locations first to be able to mark them.
Teacher: So, as any other newly embarked sailor, you'll have an empty globe at first. But as you explore, you can fill them with islands, landmarks, and many other stuff!
Teacher: Now, almost every major islands in this planet has its own local sundial similar to the one at Sundial Park. You can use those sundials to register that island on your globe permanently!
Teacher: Of course, you need to know the coordinates of the island first before registering, so that's why you need to know how to read sundials!
- Oh alright, makes sense, I guess.
Teacher: Great! Now try registering this island on your globe. Once this island has been registered, you can set this island as a Node Island.
- What is that?
Teacher: You see, it's very easy for sailors to be lost at sea. To avoid that, sailors can register a node island so that if they're lost, they can teleport back to their node island!
- Oh that's neat!
Teacher: Exactly, that's why before you ever leave this place, at least set this island as your node island first, so that if you're lost, you can always teleport back!
- Okay, then I'm ready!
Teacher: Perfect, go to the Sundial Park, register this island, and set it as your node island! After that, I'll fix your key and you'll be good to go. Good luck!
do ProgressState.progress["tutorial_island"]['teacher']['sundial_intro'] = true
=> END

~ offer_sundial_help
Teacher: Hey! How's the island registration going?
- I still need some time...
Teacher: Don't worry about it. If you don't know how to use the sundial, be sure to open the instructions that I gave you! It has everything you need there.
=> END

~ node_island_intro
Teacher: Hey you've managed to register this island on the globe!
- Of course!
Teacher: But you haven't set this island as the node island yet.
=>< node_island_help
do ProgressState.progress["tutorial_island"]['teacher']['node_island_intro'] = true
=> END

~ node_island_help
Teacher: Go back to the sundial, and then press G to register this island as your node island!
Teacher: After that, come back to me so I can fix your key, alright?
=> END

~ end_test_intro
Teacher: You did it! You have this island on the globe now!
- Yeah I do! Is that all?
Teacher: Yeah, that's all! As I promised, I will be fixing your boat key and you will be good to go now. Give me a moment...
do Common.wait(3.0)
do ProgressState.global_progress["boat_key_fixed"] = true
Teacher: There! Your key is as good as new!
Teacher: Good luck out there!
do ProgressState.progress["tutorial_island"]['teacher']['node_island_intro'] = true
do ProgressState.progress["tutorial_island"]['teacher']['end_test_intro'] = true
=> END

~ post_tutorial
Teacher: Hey! How's everything going?
- All good!
Teacher: Great!
=> END