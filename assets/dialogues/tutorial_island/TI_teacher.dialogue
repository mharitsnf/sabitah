import "res://assets/dialogues/common.dialogue" as common

~ start
if ProgressState.free_mode:
	=>< common/free_mode
	=> END

if CollectibleState.get_collectible_status("boat_key") != CollectibleState.CollectibleStatus.OBTAINED:
	if !ProgressState.get_progress(["tutorial_island", 'teacher', 'intro']):
		=>< intro
		=> END
	else:
		=>< offer_clue_help
		=> END

if !ProgressState.get_global_progress(["tutorial_island_registered"]):
	if !ProgressState.get_progress(["tutorial_island", 'teacher', 'sundial_intro']):
		=>< sundial_intro
		=> END
	else:
		=>< offer_sundial_help
		=> END

if !ProgressState.get_global_progress(['tutorial_island_set_as_node_island']):
	if !ProgressState.get_progress(["tutorial_island", 'teacher', 'node_island_intro']):
		=>< node_island_intro
		=> END
	else:
		=>< node_island_help
		=> END

if !ProgressState.get_progress(["tutorial_island", 'teacher', 'end_test_intro']):
	=>< end_test_intro
	=> END
else:
	=> post_tutorial
	=> END

=> END

~ intro
Teacher: There you are! How's your sleep?
- It's alright, I guess
- I need to sleep more, honestly.
Teacher: Hah! Classic. But hey, it's morning already, it'd be a waste to sleep the day away!
- No, I don't think so.
Teacher: Haha! You haven't changed one bit, have you?
Teacher: Anyway, I think you've heard about this, but I need your help.
- What is it?
Teacher: Long time ago, I left a treasure buried somewhere. I've always wanted to retrieve that treasure, but, you know, an old man like me can't even walk straight anymore.
Teacher: Would you help me find that treasure for me?
- I'd rather sleep.
- No.
Teacher: I'll lend you my boat.
- Where is this treasure?
Teacher: Haha! I know you've always wanted that boat.
- You got me! Alright, I'll help you out.
Teacher: Great! But before I lend you the keys, I need to test you one last time, to make sure you're truly ready for the ocean.
- What's the test gonna be?
Teacher: Lovely! The test is simple: I will give you a clue that will lead you to the boat key.
Teacher: All you need to do is to figure out where the boat key is hidden is based on the clue I provided.
Teacher: If you don't remember, I can explain to you how you can find treasures. Would you like me to do that?
- Yes please!
	=>< treasure_hunting_tutorial
- I think I'm good.
	Teacher: Confident, huh? Nice, I like it!
Teacher: I've given you the clue. You can see it in your Clues Journal.
do ClueState.change_clue_status('TI_teacher1', ClueState.ClueStatus.ACTIVE)
do Group.first("hud_layer").set_notification_text("New clue available!")
do Group.first("hud_layer").show_notification()
Teacher: Come back to me when you've found the boat key, and then I'll continue with the next test. Good luck!
do ProgressState.progress["tutorial_island"]['teacher']['intro'] = true
=> END

~ treasure_hunting_tutorial
Teacher: First thing first, see which clue you want to pursue by looking at the Clues Journal. You can access the journal through the main menu, by pressing the TAB key.
Teacher: Once you selected a clue, you'll see that it will come with pictures and description.
Teacher: Try to figure out the treasure based on the pictures and the description!
Teacher: Of course, if you've figured it out, you need to go there to dig it.
Teacher: Once you've reached the location of the treasure, press C to dig and see if your guess is correct or not!
Teacher: If your guess is correct, don't forget to set the clue status to completed in the Clues Journal! It won't change its status by itself. Remember that!
Teacher: Do you want me to explain this again?
- Yeah, maybe one more time?
	Teacher: No worries!
	=> treasure_hunting_tutorial
- Nah, I got it!
	=> END
=> END

~ offer_clue_help
Teacher: Hey buddy! How's it going? Do you need any help?
- Can you explain to me about treasure hunting again?
	=>< treasure_hunting_tutorial
- Nothing, just checking out.
	Teacher: Well, aren't you nice! But you need to focus on your test first right now.
Teacher: Good luck with the hunting! I'll be here if you need me.
=> END

~ sundial_intro
Teacher: Hey, is that what I think it is? You got the key!!!
- Of course!
Teacher: But the test is not done yet. You see, if you try to use the key on your boat, it won't work! Because I messed it up a bit, haha!
- Hey! Why'd you do that?
Teacher: Because the test is not over! I have one more thing for you, and after that, I will fix the key and you'll be good to go.
Teacher: Do you remember where you got this key? It's on an island called Sundial Park! It's called that because, well, it got a sundial on it.
Teacher: I hope you know where I'm going with this, because the next test is going to be about... using the sundials!
- Ugh, I should have known...
Teacher: Hey, sundial is a vital part of sailing! Sailors can travel around thanks to their sundials, after all.
Teacher: You do know what kind of stuff that you can do with sundials, right?
- Um... remind me again?
Teacher: Alright, listen to me carefully.
Teacher: There are three main things that you can do with the sundial.
Teacher: Firstly, sundials can act as a compass! It can tell you where the true north and south are.
Teacher: Secondly, you can find your current latitude with it as well!
Teacher: Finallly, you can find your current time, and therefore, your current longitude.
do ProgressState.global_progress['help_menu_unlocked'] = true
do ProgressState.global_progress['globe_menu_unlocked'] = true
do Group.first("hud_layer").set_notification_text("New menus unlocked: Globe and Help!")
do Group.first("hud_layer").show_notification()
Teacher: I know sundials can be a bit tricky, so I'm giving you the instructions on how to use it in the help page. Check the main menu later, okay?
- Okay... so what should I do next?
Teacher: I want you to register this island on your globe!
- What for?
Teacher: You see, every sailor has a globe filled with interesting locations. But the globes will not fill themselves automatically, you need to know the exact coordinates of those locations first to be able to mark them.
Teacher: So, as any other newly embarked sailor, you'll have an empty globe at first. But as you explore, you can fill them with islands, landmarks, and many other stuff!
Teacher: Now, almost every major islands in this planet has its own local sundial similar to the one at Sundial Park. You can use those sundials to register that island on your globe permanently!
Teacher: Of course, you need to know the coordinates of the island first before registering, so that's why you need to know how to read sundials!
- Oh alright, makes sense, I guess.
Teacher: Great! Now try registering this island on your globe. Once this island has been registered, you can then set this island as your Node Island.
- What's a Node Island?
Teacher: You see, it's very easy for sailors to be lost at sea. To avoid that, sailors can register a node island so that if they're lost, they can teleport back to their node island!
- Oh that's neat!
Teacher: Exactly, that's why before you ever leave this place, at least set this island as your node island first, so that if you're lost, you can always teleport back!
- That's convenient.
Teacher: Perfect, go to the Sundial Park, register this island, and set it as your node island! After that, I'll fix your key and you'll be good to go. Good luck!
do ProgressState.progress["tutorial_island"]['teacher']['sundial_intro'] = true
=> END

~ offer_sundial_help
Teacher: Hey! How's the island registration going?
- I still need some time...
Teacher: Don't worry about it. If you don't know how to use the sundial, be sure to open the instructions that I gave you! It has everything you need there.
=> END

~ node_island_intro
Teacher: Hey you've managed to register this island on the globe!
- Yeah, I figured it out.
Teacher: But you haven't set this island as the node island yet.
=>< node_island_help
do ProgressState.progress["tutorial_island"]['teacher']['node_island_intro'] = true
=> END

~ node_island_help
Teacher: Go back to the sundial, and then press G to register this island as your node island.
Teacher: After that, come back to me so I can fix your key, alright?
=> END

~ end_test_intro
Teacher: You did it! You have this island on the globe now!
- Of course I do! Is that all?
Teacher: Yeah, that's all! As I promised, I will be fixing your boat key and you will be good to go now. Give me a moment...
do Common.wait(3.0)
do ProgressState.global_progress["boat_key_fixed"] = true
Teacher: There! Your key is as good as new!
- Great! Thanks!
Teacher: You're welcome! Now, as for my favor...
Teacher: Like I mentioned before, I have a treasure hidden somewhere that I want to retrieve.
Teacher: Now unfortunately, I only have one part of the clue that could lead to the treasure's location.
Teacher: The other parts are currently owned by my old friends. They're somewhere out there in the world, but I'm not so sure where they currently are right now, though...
Teacher: But at least this clue will give you an idea on where you need to go first.
Teacher: Here, let me give that clue to you.
Teacher: Now, to reach my treasure, you will need to collect all of the clue parts that would lead to the treasure's location.
- Alright, how many parts are there?
Teacher: There are 4 parts in total, so you need to find 3 more!
Teacher: It might be a challenging task, but I would advise you to talk to people if you have any question.
Teacher: Everyone has a unique story to tell, and so you might learn a thing or two from them.
- Okay, I got it.
Teacher: Great! Now good luck out there! I'll be here if you ever need anything.
do ProgressState.progress["tutorial_island"]['teacher']['node_island_intro'] = true
do ProgressState.progress["tutorial_island"]['teacher']['end_test_intro'] = true
=> END

~ post_tutorial
Teacher: Hey! How's everything going?
- I'm still working on the treasure
Teacher: Great! Thank you for the help. I will be here if you ever need anything.
=> END